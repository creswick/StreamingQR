apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'pmd'

// Compile with and generate classes for Java 1.6
sourceCompatibility = 1.6
targetCompatibility = 1.6

// Library version
version = '0.1'

// Set heap size for the test JVM(s) equal to
// standard max heap for android phone
maxHeapSize = "16m"

// Look for external dependencies in these repositories
repositories {
    mavenCentral()
}


configurations {
  exported
  compile.extendsFrom exported
}

// By default, Gradle includes all of the compile time dependencies at
// runtime.
dependencies {
    // ZXing core, i.e. the QR code library
    exported "com.google.zxing:core:2.3.0"

    // Guava library -- do not export
    compile group: 'com.google.guava', name: 'guava', version: '16.+'

    // functions to export images
    testCompile "com.google.zxing:javase:2.2"

    // Junit requried to compile the unit tests
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

jar {
    // Want transitive dependencies to get resolved properly.
    // Not sure this is best way to show dependency on external library.
    manifest {
      attributes ('Implementation-Version': version,
                  'Class-Path': (configurations.runtime - configurations.exported).collect { it.getName() }.join(' '))
    }
    // Add compilation dependencies in generated Jar
    from { configurations.exported.collect {it.isDirectory() ? it : zipTree(it) } }
}

uploadArchives {
    repositories {
       flatDir {
           dirs '../android/qrstreamlib/libs'
       }
    }
}

test {
    maxHeapSize = "2048m"

    testLogging.showStandardStreams = true

    // listen to events in the test execution lifecycle
    afterTest { descriptor, result ->
       logger.lifecycle("test result: " + result + ", " + descriptor)
    }
}

// Java source code analyzer configuration
// Run 'gradle check' to invoke
pmd {
  // Available ruleSets: http://pmd.sourceforge.net/pmd-4.3.0/rules/index.html
  ruleSets = ["basic", "braces", "sunsecure" ]

}
